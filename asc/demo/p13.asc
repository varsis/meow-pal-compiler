# An ASC implementation of the following Pascal program
#
#program fact ( input, output );
#	var	n	: integer;
#	var 	sum	: integer;
#	
#	function factorial ( n : integer ): integer;
#
#		procedure sump ( n : integer );
#		begin
#			sum := sum + n;
#		end;
#
#	begin
#		if n = 0 then
#			factorial := 1
#		else
#			factorial := n * factorial ( n - 1 );
#		sump(n);
#		
#	end;
#
#begin
#	read ( n );
#
#	while n >= 0 do begin
#		sum := 0;
#		writeln ( n, '! = ', factorial ( n ), n, '+ = ', sum);
#		read ( n )
#	end;
#
#	writeln (' NEGATIVE');
#end.

#	program fact ( input, output );
#		var	n	: integer;
#		var 	sum	: integer;

	ADJUST	1	# n is at 0[0]
	ADJUST  1       # sum is at 1[0]

#	begin

start	

#		read ( n );
	READI			
	IFERR	Rerror
	POP	0[0]
#!T=100

#		while n >= 0 do begin
startwhile

	PUSH	0[0]
	CONSTI	0
	LTI
	IFNZ	endwhile

#               	sum := 0;
	CONSTI	0
	POP 	1[0]	
#				writeln ( n, '! = ', factorial ( n ), . . .
	PUSH	0[0]
	WRITEI
	CONSTI	33	# !
	WRITEC
	CONSTI	32	# sp
	WRITEC	
	CONSTI	61	# =
	WRITEC
	CONSTI	32	# sp
	WRITEC

	PUSH	0[0]		# for the parameter and the result
	CALL	1,fact
	WRITEI
				# . . . n, '+ = ', sum);

	PUSH	0[0]
	WRITEI
	CONSTI	43	# +
	WRITEC
	CONSTI	32	# sp
	WRITEC	
	CONSTI	61	# =
	WRITEC
	CONSTI	32	# sp
	WRITEC
	PUSH	1[0]
	WRITEI

	CONSTI	10	# lf
	WRITEC

#			read ( n )
	READI
	IFERR	Rerror
	POP	0[0]

#		end;

	GOTO 	startwhile

endwhile

#		writeln (' NEGATIVE');

	CONSTI	78	# NEGATIVE
	WRITEC 
	CONSTI	69
	WRITEC
	CONSTI	71
	WRITEC
	CONSTI	65
	WRITEC
	CONSTI	84
	WRITEC
	CONSTI	73
	WRITEC
	CONSTI	86
	WRITEC
	CONSTI	69
	WRITEC
	CONSTI	10
	WRITEC
	GOTO	stop

#	end.
Rerror
	CONSTI	82
	WRITEC	
	CONSTI	69
	WRITEC	
	CONSTI	65
	WRITEC	
	CONSTI	68
	WRITEC	
	CONSTI	73
	WRITEC	
	CONSTI	32
	WRITEC	
	CONSTI	69
	WRITEC	
	CONSTI	82
	WRITEC	
	CONSTI	82
	WRITEC	
	CONSTI	10
	WRITEC	

stop	STOP


#		function factorial ( n : integer ): integer;
fact	
	PUSH	-3[1]		# n			0[1]
	ADJUST	1		# for the result	1[1]
#!D
#		begin
#			if n = 0 then
	PUSH	0[1]
	IFNZ	else2

#				factorial := 1
	CONSTI	1		
	POP	1[1]
	GOTO	endif2

#			else

else2	

#				factorial := n * factorial ( n - 1 )

	PUSH	0[1]		# copy n
	PUSH	0[1]		# compute the parameter n - 1
	CONSTI	1
	SUBI
	CALL	1, fact		# compute factorial ( n - 1 )
	MULI			# *
	POP	1[1]		# :=

endif2
	
#				sump(n);
	PUSH	0[1]
	CALL	2,sump
	ADJUST	-1

#		end;

	POP	-3[1]		# return the result
	ADJUST	-1		# pop the parameter
	RET	1

#----------------------------------------------------------------------
#		procedure sump ( n : integer );

sump
	PUSH	-3[2]		#n
#		begin
#			sum := sum + n;
	PUSH	1[0]
	ADDI
	POP	1[0]

#		end;

	RET	2
